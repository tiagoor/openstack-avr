# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=glance
_relname=havana
pkgver=2013.2.b2
pkgrel=1
pkgdesc="OpenStack image registry and delivery service"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(python2-anyjson python2-argparse python2-boto python2-crypto python2-eventlet
    python2-jsonpointer python2-glanceclient python2-greenlet python2-httplib2 python2-iso8601
    python2-keystoneclient python2-kombu python2-lxml python2-migrate python2-passlib python2-paste
    python2-paste-deploy python2-pyopenssl python2-routes python2-sendfile python2-sqlalchemy
    python2-warlock python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-${pkgver: -1}/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('898f88b72d7a569e8ae0f0a78f6f5980ebfc60107121b3e0165efea5d4dd0f5e')
sha512sums=('e6af3dc343aa7c312a1b74d02c9a9f51c97a33bebec2a4f08249a4427bcd22decaf3133018990f15040c6c9c9228a44f491dc49d6274bd6ecea83054d7650d9d')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
