# Maintainer: Josh Chase <jcjoshuachase@gmail.com>

pkgname=keystone
pkgver=2012.1.2
pkgrel=1
_ostackver=essex
_clientname=python-keystoneclient
_clientver=0.1.3
pkgdesc="OpenStack identity service"
arch=('any')
license=('custom')
url="https://launchpad.net/$pkgname"
depends=('python2'
	 'python2-httplib2'
	 'python2-prettytable'
	 'python-simplejson'
	 'python-pam'
	 'python2-webob' 
	 'python2-eventlet'
	 'python2-greenlet' 
	 'python-paste'
	 'python-paste-deploy'
	 'python2-routes'
	 'python2-sqlalchemy'
	 'python2-migrate'
	 'python2-passlib'
	 'python2-lxml' )
makedepends=('python2-distribute')
source=("https://launchpad.net/$pkgname/$_ostackver/$pkgver/+download/$pkgname-$pkgver.tar.gz"
	"http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz")
md5sums=('026dfa10e37108b75b92d39cbc449bd0'
	 'da52d237390404e4d52f26354357fa8e')

build() {
  cd $srcdir

  find . -type f -exec sed -i -e 's|^#!/usr/bin/python$|#!/usr/bin/python2|' \
    -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' {} +

  cd $srcdir/$_clientname-$_clientver
  python2 setup.py build

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd $srcdir/$_clientname-$_clientver
  python2 setup.py install --root=$pkgdir

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  
  install -dm600 "$pkgdir"/etc/$pkgname
  install -Dm600 etc/* "$pkgdir"/etc/$pkgname/
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
